apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

//获取当前日期
def static releaseTime() {
    return new Date().format("yyyy-MM-dd")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion


    defaultConfig {
        applicationId "com.hjc.wan"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        ndk {
            // 设置支持的SO库架构(必须设置如下几种,否则X5会初始化失败)
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'BASE_URL', '"https://www.wanandroid.com"'
            buildConfigField 'boolean', 'IS_DEBUG', "false"
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'BASE_URL', '"https://www.wanandroid.com"'
            buildConfigField 'boolean', 'IS_DEBUG', "true"
        }
    }

    //指定打包路径和apk名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName = "Wan_V${variant.versionName}"
            fileName = fileName + "_${releaseTime()}"
            if ('debug' == variant.buildType.name) {
                fileName = fileName + "_debug.apk"
            } else {
                fileName = fileName + "_release.apk"
            }
            outputFileName = fileName
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        checkReleaseBuilds false
        abortOnError false
    }

    buildFeatures {
        viewBinding = true
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'

    //分包处理
    implementation 'androidx.multidex:multidex:2.0.1'

    //图片处理框架
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'jp.wasabeef:glide-transformations:4.0.1'

    //权限
    implementation 'com.permissionx.guolindev:permissionx:1.4.0'

    //Bugly
    implementation 'com.tencent.bugly:crashreport:3.2.1'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'

    //列表适配器
    //noinspection GradleDependency
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'

    //路由
    kapt 'com.alibaba:arouter-compiler:1.2.2'

    //Banner(不升级,改动较大)
    //noinspection GradleDependency
    implementation 'com.youth.banner:banner:1.4.10'

    //指示器
    implementation 'com.github.hackware1993:MagicIndicator:1.6.0'

    //流式布局
    implementation 'com.nex3z:flow-layout:1.2.4'

    //dialog
    implementation 'com.afollestad.material-dialogs:core:3.1.1'
    implementation 'com.afollestad.material-dialogs:color:3.1.1'
    implementation 'com.afollestad.material-dialogs:datetime:3.1.1'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.1.1'

    //Room
    implementation "androidx.room:room-runtime:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    //刷新
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.2'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.2'

    implementation project(path: ':BaseLib')
    implementation project(path: ':WebViewLib')
}
